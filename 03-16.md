# 03/16 

### Carry over from yesterday

Working on understanding how may-writes are modeled by Polly.

#### `testbed.ll`
```ll
; currently treat all may-writes as must-writes, should not be the case
; RUN: opt %loadPolly -analyze < %s | FileCheck %s
;
; FIXME: Edit the run line and add checks!
;
; XFAIL: *
;
;    static const int N = 3000;
;
;    void f(int A[N], int B[N]) {
;      for (int i = 0; i < N; i++) {
;        //__sync_synchronize();
;        A[i] = 20;
;
;        if (i * i) {
;          //__sync_synchronize();
;          A[i] = 90;
;        }
;
;        //__sync_synchronize();
;        B[i] = A[i];
;        A[i] = 42;
;      }
;    }
;
source_filename = "testbed.c"
target datalayout = "e-m:o-i64:64-f80:128-n8:16:32:64-S128"

define void @f(i32* %A, i32* %B) {
entry:
  br label %for.cond

for.cond:                                         ; preds = %for.inc, %entry
  %indvars.iv = phi i64 [ %indvars.iv.next, %for.inc ], [ 0, %entry ]
  %exitcond = icmp ne i64 %indvars.iv, 3000
  br i1 %exitcond, label %for.body, label %for.end

for.body:                                         ; preds = %for.cond
  %arrayidx = getelementptr inbounds i32, i32* %A, i64 %indvars.iv
  store i32 20, i32* %arrayidx, align 4
  br label %for.new_bb

for.new_bb:
  %tmp = mul nsw i64 %indvars.iv, %indvars.iv
  %tmp2 = trunc i64 %tmp to i32
  %tobool = icmp eq i32 %tmp2, 0
  br i1 %tobool, label %if.end, label %if.then

if.then:                                          ; preds = %for.body
  %arrayidx2 = getelementptr inbounds i32, i32* %A, i64 %indvars.iv
  store i32 90, i32* %arrayidx2, align 4
  br label %if.end

if.end:                                           ; preds = %for.body, %if.then
  %arrayidx4 = getelementptr inbounds i32, i32* %A, i64 %indvars.iv
  %tmp3 = load i32, i32* %arrayidx4, align 4
  %arrayidx6 = getelementptr inbounds i32, i32* %B, i64 %indvars.iv
  store i32 %tmp3, i32* %arrayidx6, align 4
  ; store i32 42, i32* %arrayidx4, align 4
  br label %for.inc

for.inc:                                          ; preds = %if.end
  %indvars.iv.next = add nuw nsw i64 %indvars.iv, 1
  br label %for.cond

for.end:                                          ; preds = %for.cond
  ret void
}
```

notice the line:

```ll
  ; store i32 42, i32* %arrayidx4, align 4
```


##### If this line is *disabled*, then the output of `-polly-ast` is:
```cpp
if (1 && (&MemRef_A[3000] <= &MemRef_B[0] || &MemRef_B[3000] <= &MemRef_A[0]))

    for (int c0 = 0; c0 <= 2999; c0 += 1) {
      Stmt_for_body(c0);
      Stmt_for_new_bb__TO__if_end(c0);
      Stmt_if_end(c0);
    }

else
    {  /* original code */ }
```

##### If this line is *enabled*, then the output is:
```cpp
if (1 && (&MemRef_A[3000] <= &MemRef_B[0] || &MemRef_B[3000] <= &MemRef_A[0]))

    for (int c0 = 0; c0 <= 2999; c0 += 1) {
      Stmt_for_new_bb__TO__if_end(c0);
      Stmt_if_end(c0);
    }

else
    {  /* original code */ }
```

#### Why does this happen?

- The llvm statement that we are using is a must-write onto array `A`.
- Roughly, the code looks something like
    ```cpp
        void f(int A[N], int B[N]) {
          for (int i = 0; i < N; i++) {
            //__sync_synchronize();
            A[i] = 20;

            if (i * i) {
              //__sync_synchronize();
              A[i] = 90;
            }

            //__sync_synchronize();
            B[i] = A[i];
            //llvm statement
            A[i] = 42; // <=
          }
        }

    ```

when dead code elimination runs, it sees that the statement `A[i] = 20` is a 
write into A. However, `A[i] = 42` is also a write into `A`. Hence, it
chooses to remove the `A[i] = 20`, not noticing that this affects the write
into `B[i]`.


### Work today

- What happens if we remove the may-write?

#####  Dead code elimination is not using must-writes correctly?

```cpp
; currently treat all may-writes as must-writes, should not be the case
; RUN: opt %loadPolly -analyze < %s | FileCheck %s
;
; FIXME: Edit the run line and add checks!
;
; XFAIL: *
;
;    static const int N = 3000;
;
;    void f(int A[N], int B[N]) {
;      for (int i = 0; i < N; i++) {
;        //__sync_synchronize();
;        A[i] = 20;
;
;        //__sync_synchronize();
;        A[i] = 90;
;
;        //__sync_synchronize();G
;        B[i] = A[i];
;        A[i] = 42;
;      }
;    }
;
source_filename = "testbed.c"
target datalayout = "e-m:o-i64:64-f80:128-n8:16:32:64-S128"

define void @f(i32* %A, i32* %B) {
entry:
  br label %for.cond

for.cond:                                         ; preds = %for.inc, %entry
  %indvars.iv = phi i64 [ %indvars.iv.next, %for.inc ], [ 0, %entry ]
  %exitcond = icmp ne i64 %indvars.iv, 3000
  br i1 %exitcond, label %write.a.20, label %for.end

write.a.20:                                         ; preds = %for.cond
  %arrayidx = getelementptr inbounds i32, i32* %A, i64 %indvars.iv
  store i32 20, i32* %arrayidx, align 4
  br label %for.new_bb

for.new_bb:
  %tmp = mul nsw i64 %indvars.iv, %indvars.iv
  %tmp2 = trunc i64 %tmp to i32
  %tobool = icmp eq i32 %tmp2, 0
  ; br i1 %tobool, label %write.b.from.a, label %write.a.90
  br label %write.a.90

write.a.90:                                          ; preds = %write.a.20
  %arrayidx2 = getelementptr inbounds i32, i32* %A, i64 %indvars.iv
  store i32 90, i32* %arrayidx2, align 4
  br label %write.b.from.a

write.b.from.a:                                           ; preds = %write.a.20, %if.then
  %arrayidx4 = getelementptr inbounds i32, i32* %A, i64 %indvars.iv
  %tmp3 = load i32, i32* %arrayidx4, align 4
  %arrayidx6 = getelementptr inbounds i32, i32* %B, i64 %indvars.iv
  store i32 %tmp3, i32* %arrayidx6, align 4
  br label %a.write.42
  ; br label %for.inc
  
a.write.42:
  store i32 42, i32* %arrayidx4, align 4
  br label %for.inc

for.inc:                                          ; preds = %write.b.from.a
  %indvars.iv.next = add nuw nsw i64 %indvars.iv, 1
  br label %for.cond

for.end:                                          ; preds = %for.cond
  ret void
}
```

##### generated AST

```cpp
if (1 && (&MemRef_A[3000] <= &MemRef_B[0] || &MemRef_B[3000] <= &MemRef_A[0]))

    for (int c0 = 0; c0 <= 2999; c0 += 1) {
      Stmt_write_b_from_a(c0);
      Stmt_a_write_42(c0);
    }

else
    {  /* original code */ }

```

- This eliminates the `A[i] = 90` AND `A[i] = 20` which is incorrect. 

- `A[i] = 20` can be removed since it is eclipsed by `A[i] = 90` 
which is correct.

- `A[i] = 90` should not be removed as there is a side-effect (the write to `B[i]`) which
depends on this write.

- Going to read through `DeadCodeElimination.cpp` to see what's happening

## side effects of writes are not taken into account during dead code elimination
##### minimal flawed example

```cpp
; currently treat all may-writes as must-writes, should not be the case
; RUN: opt %loadPolly -analyze < %s | FileCheck %s
;
; FIXME: Edit the run line and add checks!
;
; XFAIL: *
;
;    static const int N = 3000;
;
;    void f(int A[N], int B[N]) {
;      for (int i = 0; i < N; i++) {
;        A[i] = 20;
;        B[i] = A[i];
;        A[i] = 42;
;      }
;    }
;
source_filename = "testbed.c"
target datalayout = "e-m:o-i64:64-f80:128-n8:16:32:64-S128"

define void @f(i32* %A, i32* %B) {
entry:
  br label %for.cond

for.cond:                                         ; preds = %for.inc, %entry
  %indvars.iv = phi i64 [ %indvars.iv.next, %for.inc ], [ 0, %entry ]
  %exitcond = icmp ne i64 %indvars.iv, 3000
  br i1 %exitcond, label %write.a.20, label %for.end

write.a.20:                                         ; preds = %for.cond
  %arrayidx = getelementptr inbounds i32, i32* %A, i64 %indvars.iv
  store i32 20, i32* %arrayidx, align 4
  ;br label %for.new_bb
  br label %write.b.from.a
; for.new_bb:
;   %tmp = mul nsw i64 %indvars.iv, %indvars.iv
;   %tmp2 = trunc i64 %tmp to i32
;   %tobool = icmp eq i32 %tmp2, 0
;   ; br i1 %tobool, label %write.b.from.a, label %write.a.90
;   br label %write.a.90

; write.a.90:                                          ; preds = %write.a.20
;   %arrayidx2 = getelementptr inbounds i32, i32* %A, i64 %indvars.iv
;   store i32 90, i32* %arrayidx2, align 4
;   br label %write.b.from.a

write.b.from.a:                                           ; preds = %write.a.20, %if.then
  %arrayidx4 = getelementptr inbounds i32, i32* %A, i64 %indvars.iv
  %tmp3 = load i32, i32* %arrayidx4, align 4
  %arrayidx6 = getelementptr inbounds i32, i32* %B, i64 %indvars.iv
  store i32 %tmp3, i32* %arrayidx6, align 4
  br label %a.write.42
  ; br label %for.inc

a.write.42:
  %arrayidx5 = getelementptr inbounds i32, i32* %A, i64 %indvars.iv
  store i32 42, i32* %arrayidx5, align 4
  br label %for.inc

for.inc:                                          ; preds = %write.b.from.a
  %indvars.iv.next = add nuw nsw i64 %indvars.iv, 1
  br label %for.cond

for.end:                                          ; preds = %for.cond
  ret void
}

```

##### Generated AST
```
if (1 && (&MemRef_A[3000] <= &MemRef_B[0] || &MemRef_B[3000] <= &MemRef_A[0]))

    for (int c0 = 0; c0 <= 2999; c0 += 1) {
      Stmt_write_b_from_a(c0);
      Stmt_a_write_42(c0);
    }

else
    {  /* original code */ }
```
##### Polly info
```
Printing analysis 'Polly - Create polyhedral description of Scops' for region: 'for.cond => for.end' in function 'f':
    Function: f
    Region: %for.cond---%for.end
    Max Loop Depth:  1
    Invariant Accesses: {
    }
    Context:
    {  :  }
    Assumed Context:
    {  :  }
    Invalid Context:
    {  : 1 = 0 }
    Arrays {
        i32 MemRef_A[*]; // Element size 4
        i32 MemRef_B[*]; // Element size 4
    }
    Arrays (Bounds as pw_affs) {
        i32 MemRef_A[*]; // Element size 4
        i32 MemRef_B[*]; // Element size 4
    }
    Alias Groups (1):
        [[ <{ MemRef_B[(0)] }, { MemRef_B[(3000)] }> <{ MemRef_A[(0)] }, { MemRef_A[(3000)] }> ]]
    Statements {
    	Stmt_write_a_20
            Domain :=
                { Stmt_write_a_20[i0] : 0 <= i0 <= 2999 };
            Schedule :=
                { Stmt_write_a_20[i0] -> [i0, 0] };
            MustWriteAccess :=	[Reduction Type: NONE] [Scalar: 0]
                { Stmt_write_a_20[i0] -> MemRef_A[i0] };
    	Stmt_write_b_from_a
            Domain :=
                { Stmt_write_b_from_a[i0] : 0 <= i0 <= 2999 };
            Schedule :=
                { Stmt_write_b_from_a[i0] -> [i0, 1] };
            ReadAccess :=	[Reduction Type: NONE] [Scalar: 0]
                { Stmt_write_b_from_a[i0] -> MemRef_A[i0] };
            MustWriteAccess :=	[Reduction Type: NONE] [Scalar: 0]
                { Stmt_write_b_from_a[i0] -> MemRef_B[i0] };
    	Stmt_a_write_42
            Domain :=
                { Stmt_a_write_42[i0] : 0 <= i0 <= 2999 };
            Schedule :=
                { Stmt_a_write_42[i0] -> [i0, 2] };
            MustWriteAccess :=	[Reduction Type: NONE] [Scalar: 0]
                { Stmt_a_write_42[i0] -> MemRef_A[i0] };
    }
Printing analysis 'Polly - Calculate dependences' for region: 'for.cond => for.end' in function 'f':
	RAW dependences:
		{ Stmt_write_a_20[i0] -> Stmt_write_b_from_a[i0] : 0 <= i0 <= 2999 }
	WAR dependences:
		{ Stmt_write_b_from_a[i0] -> Stmt_a_write_42[i0] : 0 <= i0 <= 2999; Stmt_write_a_20[i0] -> Stmt_a_write_42[i0] : 0 <= i0 <= 2999 }
	WAW dependences:
		{  }
	Reduction dependences:
		n/a
	Transitive closure of reduction dependences:
		{  }
```

## Problem (Found)

- `isl_union_map_union` with a `nullptr` returns a `nullptr`
- `RED` in `DependenceInfo.cpp` is set to `NULL` by default, it should actually be the empty space.
- `getLiveOut` in `DeadCodeElimination.cpp` asks for `RAW | RED`.
- `Dependences` tries to deliver. Tries to union `RED` (which is empty). Gets trashed (set to `NULL`).
- Computation of live outs is incorrect
- It is unable to understand the RAW access that occured. 
- Test case fails.


## Solution

- set `RED` to the correct empty space in `DependenInfo.cpp::collectInfo()`  if `!HasReductions()`

## Trivia stuff learnt
- `llvm-lit` is used to run tests in LLVM and in Polly
- need to symlink `lit.site.cfg` from `llvm_build/...polly/.../lit.site.cfg`
