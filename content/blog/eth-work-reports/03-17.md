+++
Categories = ["polly"]
Description = ""
Tags = ["polly"]
date = "2017-03-17"
title = "Work report: March 17"
+++


## Carry over from yesterday
- Commit into LLVM made [commit `58a4c1`](https://reviews.llvm.org/D31036)
- Need to check if may-writes are handled correctly after change landed

## Work today

### May writes still handled incorrectly after patch for incorrect creation of dependence information

##### failing `testbed.ll`

```cpp
; RUN: opt %loadPolly -analyze < %s | FileCheck %s
;
; FIXME: Edit the run line and add checks!
;
; XFAIL: *
;
;    static const int N = 3000;
;
;    void f(int A[N], int B[N]) {
;      for (int i = 0; i < N; i++) {
;        //__sync_synchronize();
;        A[i] = 20;
;
;        if (i * i) {
;          //__sync_synchronize();
;          A[i] = 90;
;        }
;
;        //__sync_synchronize();
;        B[i] = A[i];
;      }
;      A[i] = 42;
;    }
;
source_filename = "testbed.c"
target datalayout = "e-m:o-i64:64-f80:128-n8:16:32:64-S128"

define void @f(i32* %A, i32* %B) {
entry:
  br label %for.cond

for.cond:                                         ; preds = %for.inc, %entry
  %indvars.iv = phi i64 [ %indvars.iv.next, %for.inc ], [ 0, %entry ]
  %exitcond = icmp ne i64 %indvars.iv, 3000
  br i1 %exitcond, label %A.must.write.20, label %for.end

A.must.write.20:
  %arrayidx = getelementptr inbounds i32, i32* %A, i64 %indvars.iv
  store i32 20, i32* %arrayidx, align 4
  br label %compute.i.square

compute.i.square:
  %tmp = mul nsw i64 %indvars.iv, %indvars.iv
  %tmp2 = trunc i64 %tmp to i32
  %tobool = icmp eq i32 %tmp2, 0
  br i1 %tobool, label %B.write.from.A, label %A.may.write.90

A.may.write.90:
  %arrayidx2 = getelementptr inbounds i32, i32* %A, i64 %indvars.iv
  store i32 90, i32* %arrayidx2, align 4
  br label %B.write.from.A

B.write.from.A:
  %arrayidx4 = getelementptr inbounds i32, i32* %A, i64 %indvars.iv
  %tmp3 = load i32, i32* %arrayidx4, align 4
  %arrayidx6 = getelementptr inbounds i32, i32* %B, i64 %indvars.iv
  store i32 %tmp3, i32* %arrayidx6, align 4
  br label %A.must.write.42
  ; br label %for.inc

A.must.write.42:
  %arrayidx5 = getelementptr inbounds i32, i32* %A, i64 %indvars.iv
  store i32 42, i32* %arrayidx5, align 4
  br label %for.inc

for.inc:                                          ; preds = %if.end
  %indvars.iv.next = add nuw nsw i64 %indvars.iv, 1
  br label %for.cond

for.end:                                          ; preds = %for.cond
  ret void
}
```

##### AST Output for `testbed.ll`

```
if (1 && (&MemRef_A[3000] <= &MemRef_B[0] || &MemRef_B[3000] <= &MemRef_A[0]))

    for (int c0 = 0; c0 <= 2999; c0 += 1) {
      Stmt_compute_i_square__TO__B_write_from_A(c0);
      Stmt_B_write_from_A(c0);
      Stmt_A_must_write_42(c0);
    }

else
    {  /* original code */ }
```

Clearly, this is wrong because the must-write of `A[i] = 20` is not being used even though
this has a side-effect (on the write of `B[i]`).

##### Dependence information for `testbed.ll`

##### Fix
- Make sure that you're actually generating may-writes `:)`.
- Edited `DependenceInfo.cpp` so that may writes work correctly.
- also take may-write info from the flow info that we get.

### Fix deprecated `isl` call in Polly
- replace `isl_dim_n_out` with `isl_map_dim(*, isl_dim_out)`

### Model `must-writes` as `may-writes` for reduction writes, since we need these to always stay, rather than using *transitive closure* in `addPrivatizationDependences()`

- transitive closure is expensive to compure.
- adding may-writes simplifies life by a lot.
- now that may-writes work correctly, this can simplify performance costs.

##### This breaks a whole lot of test cases `:)`

### Case study of breaking: `dead_iteration_elimination.ll`

```cpp
; RUN: opt -S %loadPolly -basicaa -polly-dependences-analysis-type=value-based -polly-dce -polly-dce-precise-steps=2 -polly-ast -analyze < %s | FileCheck %s
target datalayout = "e-p:64:64:64-i1:8:8-i8:8:8-i16:16:16-i32:32:32-i64:64:64-f32:32:32-f64:64:64-v64:64:64-v128:128:128-a0:0:64-s0:64:64-f80:128:128-n8:16:32:64"
;
; for(i = 0; i < 200; i++ )
;   A[i] = 2;
;
; for (i = 0; i < 50; i++ )
;   A[i]  = 5;
;
; for (i = 0; i < 70; i++ )
;   A[i] = A[i] + 5;
;
; for (i = 100; i < 110; i++ )
;   A[i] = i;
;
define void @main() nounwind uwtable {
entry:
  %A = alloca [200 x i32], align 16
  br label %for.body.1

for.body.1:
  %indvar.1 = phi i64 [ 0, %entry ], [ %indvar.next.1, %for.body.1 ]
  %arrayidx.1 = getelementptr [200 x i32], [200 x i32]* %A, i64 0, i64 %indvar.1
  store i32 2, i32* %arrayidx.1, align 4
  %indvar.next.1 = add i64 %indvar.1, 1
  %exitcond.1 = icmp ne i64 %indvar.next.1, 200
  br i1 %exitcond.1, label %for.body.1, label %exit.1

exit.1:
  br label %for.body.2

for.body.2:
  %indvar.2 = phi i64 [ 0, %exit.1 ], [ %indvar.next.2, %for.body.2 ]
  %arrayidx.2 = getelementptr [200 x i32], [200 x i32]* %A, i64 0, i64 %indvar.2
  store i32 5, i32* %arrayidx.2, align 4
  %indvar.next.2 = add i64 %indvar.2, 1
  %exitcond.2 = icmp ne i64 %indvar.next.2, 50
  br i1 %exitcond.2, label %for.body.2, label %exit.2

exit.2:
  br label %for.body.3

for.body.3:
  %indvar.3 = phi i64 [ 0, %exit.2 ], [ %indvar.next.3, %for.body.3 ]
  %arrayidx.3 = getelementptr [200 x i32], [200 x i32]* %A, i64 0, i64 %indvar.3
  %val = load i32, i32* %arrayidx.3, align 4
  %add = add nsw i32 %val, 5
  store i32 %add, i32* %arrayidx.3, align 4
  %indvar.next.3 = add i64 %indvar.3, 1
  %exitcond.3 = icmp ne i64 %indvar.next.3, 70
  br i1 %exitcond.3, label %for.body.3 , label %exit.3

exit.3:
  br label %for.body.4

for.body.4:
  %indvar.4 = phi i64 [ 0, %exit.3 ], [ %indvar.next.4, %for.body.4 ]
  %indvar.plus = add i64 %indvar.4, 100
  %trunc = trunc i64 %indvar.plus to i32
  %arrayidx.4 = getelementptr [200 x i32], [200 x i32]* %A, i64 0, i64 %indvar.plus
  store i32 %trunc, i32* %arrayidx.4, align 4
  %indvar.next.4 = add i64 %indvar.4, 1
  %exitcond.4 = icmp ne i64 %indvar.next.4, 10
  br i1 %exitcond.4, label %for.body.4, label %exit.4

exit.4:
  ret void
}

; CHECK: for (int c0 = 50; c0 <= 99; c0 += 1)
; CHECK:   Stmt_for_body_1(c0);
; CHECK: for (int c0 = 110; c0 <= 199; c0 += 1)
; CHECK:   Stmt_for_body_1(c0);
; CHECK: for (int c0 = 0; c0 <= 49; c0 += 1)
; CHECK:   Stmt_for_body_2(c0);
; CHECK: for (int c0 = 0; c0 <= 69; c0 += 1)
; CHECK:   Stmt_for_body_3(c0);
; CHECK: for (int c0 = 0; c0 <= 9; c0 += 1)
```

##### Our output
```cpp
    {
      for (int c0 = 0; c0 <= 99; c0 += 1)
        Stmt_for_body_1(c0);
      for (int c0 = 110; c0 <= 199; c0 += 1)
        Stmt_for_body_1(c0);
      for (int c0 = 0; c0 <= 49; c0 += 1)
        Stmt_for_body_2(c0);
      for (int c0 = 0; c0 <= 69; c0 += 1)
        Stmt_for_body_3(c0);
      for (int c0 = 0; c0 <= 9; c0 += 1)
        Stmt_for_body_4(c0);
    }

else
    {  /* original code */ }

```

- the writes from `0` to `50` are not tracked in our code
- `c0` should start from 50, since `0-50` is overwritten in the second loop.
- It is **incorrectly** starting from 0

##### Difference in generated dependence information

###### Original (correct)
```
    Statements {
    	Stmt_for_body_1
            Domain :=
                { Stmt_for_body_1[i0] : 0 <= i0 <= 199 };
            Schedule :=
                { Stmt_for_body_1[i0] -> [0, i0] };
            MustWriteAccess :=	[Reduction Type: NONE] [Scalar: 0]
                { Stmt_for_body_1[i0] -> MemRef_A[i0] };
    	Stmt_for_body_2
            Domain :=
                { Stmt_for_body_2[i0] : 0 <= i0 <= 49 };
            Schedule :=
                { Stmt_for_body_2[i0] -> [1, i0] };
            MustWriteAccess :=	[Reduction Type: NONE] [Scalar: 0]
                { Stmt_for_body_2[i0] -> MemRef_A[i0] };
    	Stmt_for_body_3
            Domain :=
                { Stmt_for_body_3[i0] : 0 <= i0 <= 69 };
            Schedule :=
                { Stmt_for_body_3[i0] -> [2, i0] };
            ReadAccess :=	[Reduction Type: +] [Scalar: 0]
                { Stmt_for_body_3[i0] -> MemRef_A[i0] };
            MustWriteAccess :=	[Reduction Type: +] [Scalar: 0]
                { Stmt_for_body_3[i0] -> MemRef_A[i0] };
    	Stmt_for_body_4
            Domain :=
                { Stmt_for_body_4[i0] : 0 <= i0 <= 9 };
            Schedule :=
                { Stmt_for_body_4[i0] -> [3, i0] };
            MustWriteAccess :=	[Reduction Type: NONE] [Scalar: 0]
                { Stmt_for_body_4[i0] -> MemRef_A[100 + i0] };
    }
    
	RAW dependences:
		{ Stmt_for_body_2[i0] -> Stmt_for_body_3[i0] : 0 <= i0 <= 49;
      Stmt_for_body_1[i0] -> Stmt_for_body_3[i0] : 50 <= i0 <= 69 }
	WAR dependences:
		{  }
	WAW dependences:
	  {
	    Stmt_for_body_1[i0] -> Stmt_for_body_4[-100 + i0] : 100 <= i0 <= 109;
       Stmt_for_body_1[i0] -> Stmt_for_body_2[i0] : 0 <= i0 <= 49;
       Stmt_for_body_1[i0] -> Stmt_for_body_3[i0] : 50 <= i0 <= 69;
       Stmt_for_body_2[i0] -> Stmt_for_body_3[i0] : 0 <= i0 <= 49
      }
	Reduction dependences:
		{  }
	Transitive closure of reduction dependences:
		n/a
```
###### Ours (incorrect)
```
   Statements {
    	Stmt_for_body_1
            Domain :=
                { Stmt_for_body_1[i0] : 0 <= i0 <= 199 };
            Schedule :=
                { Stmt_for_body_1[i0] -> [0, i0] };
            MustWriteAccess :=	[Reduction Type: NONE] [Scalar: 0]
                { Stmt_for_body_1[i0] -> MemRef_A[i0] };
    	Stmt_for_body_2
            Domain :=
                { Stmt_for_body_2[i0] : 0 <= i0 <= 49 };
            Schedule :=
                { Stmt_for_body_2[i0] -> [1, i0] };
            MustWriteAccess :=	[Reduction Type: NONE] [Scalar: 0]
                { Stmt_for_body_2[i0] -> MemRef_A[i0] };
    	Stmt_for_body_3
            Domain :=
                { Stmt_for_body_3[i0] : 0 <= i0 <= 69 };
            Schedule :=
                { Stmt_for_body_3[i0] -> [2, i0] };
            ReadAccess :=	[Reduction Type: +] [Scalar: 0]
                { Stmt_for_body_3[i0] -> MemRef_A[i0] };
            MustWriteAccess :=	[Reduction Type: +] [Scalar: 0]
                { Stmt_for_body_3[i0] -> MemRef_A[i0] };
    	Stmt_for_body_4
            Domain :=
                { Stmt_for_body_4[i0] : 0 <= i0 <= 9 };
            Schedule :=
                { Stmt_for_body_4[i0] -> [3, i0] };
            MustWriteAccess :=	[Reduction Type: NONE] [Scalar: 0]
                { Stmt_for_body_4[i0] -> MemRef_A[100 + i0] };
    }
    
	RAW dependences:
		{ Stmt_for_body_2[i0] -> Stmt_for_body_3[i0] : 0 <= i0 <= 49;
      Stmt_for_body_1[i0] -> Stmt_for_body_3[i0] : 0 <= i0 <= 69
    }
	WAR dependences:
		{  }
	WAW dependences:
		{  }
	Reduction dependences:
		{  }
	Transitive closure of reduction dependences:
		n/a
```

###### Fix 1 - create may-writes for reductions
- where we create may-writes in `DependenceInfo.cpp::collectInfo()`, check that the memory access is a possible reduction by using `MA->isReductionLike()` and add may-writes. 

##### Note: Paper to read - `Polyhedral AST generation is more than scanning polyhedra`