# MM/DD

## Carry over from yesterday
## Work today
- Got unified memory working
- difference between unified memory & unified virtual address space:

##### Unified virtual addresss space
+ seems to be more about `device <-> device`
+ shared _address space_ b/w `device <-> device` & `device <-> host`
+ devices can share pointers without copying between devices
+ host and device still need a `cudaMalloc` 

##### Unified Memory
+ seems to be more about `device <-> host`
+ "unified memory" allows host and device to share memory transparently.
+ will be more useful as more hardware support comes along
+ page level granularity for caching etc
+ unified memory just needs you to **allocate** managed memory with `cudaMallocManaged`
+ no `cudaMemcpy` overheads


```cpp
#include <stdio.h>
#include <cuda_runtime.h>
#include <assert.h>
#include <stdlib.h>
#include <time.h>
#include <ncurses/curses.h>
#include <unistd.h>

#define NUM_COLORS 10
#define DELAY 30000

#define ROWS 80
#define COLS 80

//Macro for checking cuda errors following a cuda launch or api call
#define cudaCheckError() {                                          \
    cudaError_t e=cudaGetLastError();                                 \
    if(e!=cudaSuccess) {                                              \
        printf("Cuda failure %s:%d: '%s'\n",__FILE__,__LINE__,cudaGetErrorString(e));           \
        exit(0); \
    }                                                                 \
}

#define get(arr, x, y) (arr)[((x) % ROWS) * COLS + ((y) % COLS)]
#define set(arr, x, y, val) (arr)[((x) % ROWS) * COLS + ((y) % COLS)] = val;

/*
__device__  int pingpong[2][ROWS * COLS];
__device__  int i;
*/
__global__ void NaiveStep(int *oldGrid, int *newGrid)
{
    //return;

    const int x = blockIdx.x*blockDim.x+threadIdx.x;
    const int y = blockIdx.y*blockDim.y+threadIdx.y;


    if (x >= 0 && y >= 0 && x < ROWS && y < COLS) {
        const int old = get(oldGrid, x, y);
        const int wantedNext = (old + 1) % NUM_COLORS;
        int dx[] = {1, 0, -1, 0};
        int dy[] = {0, 1, 0, -1};


        set(newGrid, x, y,  old);
        // set(newGrid, x, y, (old + 1) % NUM_COLORS);
        // return;
        for(int i = 0; i < 4; i++) {
            int ix = (x + dx[i]) % ROWS;
            ix = ix < 0 ? ix + ROWS : ix;

            int iy = (y + dy[i]) % COLS;
            iy = iy < 0 ? iy + COLS : iy;

            if (get(oldGrid, ix, iy) == wantedNext) {
                set(newGrid, x, y, wantedNext);
                break;
            }
        }


    }
}

void print_automata(int *grid) {
    static const char outs[] = " .:-=+*#%@";
    assert(sizeof(outs) / sizeof(char) >= NUM_COLORS && "do not have enough "
            "characters to print!");

    for(int i = 0; i < ROWS; i++) {
        for(int j = 0; j < COLS; j++) {
            const char ch = outs[grid[i * COLS + j]];
            mvaddch(i, j, ch);
        }
    }
}

void init_automata(int *automata) {
    for (int r = 0; r < ROWS; r++) {
        for(int c = 0; c < COLS; c++) {
            automata[r * COLS + c] = rand() % NUM_COLORS;
        }
    }
}

int main(void)
{
    int *pingpong[2];

    for(int i = 0; i < 2; i++) {
        cudaMallocManaged(&pingpong[i], ROWS * COLS * sizeof(int));
        cudaCheckError();
    }
    printf("cudaMallocManaged succceeded...\n");
    srand(time(NULL));
    init_automata(pingpong[0]);

    static const int THREADS_ALONG_AXIS = 10;
    dim3 dimBlock(ROWS / THREADS_ALONG_AXIS, COLS / THREADS_ALONG_AXIS);
    dim3 dimGrid(THREADS_ALONG_AXIS, THREADS_ALONG_AXIS);


    initscr();
    noecho();
    curs_set(FALSE);

   int i = 0;
   do {
        for(int _ = 0; _ < 50; _++) {
            const int newi = (i + 1) % 2;
            NaiveStep<<<dimGrid, dimBlock>>>(pingpong[i],
                                             pingpong[newi]);
            cudaCheckError();
            cudaDeviceSynchronize();
            cudaCheckError();


            if (false){
                const int randr = rand() % ROWS;
                const int randc = rand() % COLS;
                const int WINDOW_HALFDIM = 4;

                for(int dr = -WINDOW_HALFDIM; dr <= WINDOW_HALFDIM; dr++) {
                    for(int dc = -WINDOW_HALFDIM; dc <= WINDOW_HALFDIM; dc++) {
                        set(pingpong[newi], randr + dr, randc + dc,
                                rand() % NUM_COLORS);
                    }
                }

            }

            clear();
            print_automata(pingpong[i]);
            refresh();
            usleep(DELAY);

            i = newi;
        }
    } while(getchar() != 'q');

    endwin();
    for(int i = 0; i < 2; i++) {
        cudaFree(pingpong[i]);
    }
    cudaDeviceReset();
}
```